/**
* Jenkins pipeline to build a library.
*
* This pipeline requires the following credentials:
* ---
* Type          | ID                | Description
* Secret text   | nexus-base-url    | The Nexus instance base URL. E.g. https://nexus.subatomic.local/content/repositories/
* Secret file   | maven-settings    | Maven settings.xml
*
*/
node('maven') {

    withCredentials([
            string(credentialsId: 'nexus-base-url', variable: 'NEXUS_BASE_URL'),
            file(credentialsId: 'maven-settings', variable: 'MVN_SETTINGS'),
    ]) {
        stage('Checks and Tests') {
            checkout(scm)

            try {
                sh ': Maven build && ./mvnw --batch-mode verify --settings $MVN_SETTINGS'
            } finally {
                junit 'target/surefire-reports/*.xml'
            }
        }

        if (env.BRANCH_NAME == 'master' || !env.BRANCH_NAME) {
            stage('Publish to Nexus') {
                repository = 'releases'
                pom = readMavenPom file: 'pom.xml'
                if (pom.version.endsWith('SNAPSHOT')) {
                    repository = 'snapshots'
                }

                sh ': Maven deploy && ./mvnw --batch-mode deploy -DskipTests ' +
                        "-DaltDeploymentRepository=nexus::default::${env.NEXUS_BASE_URL}/${repository}/ " +
                        '--settings $MVN_SETTINGS'
            }
        }
    }
}

